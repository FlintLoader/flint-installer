plugins {
    id 'java'
    id 'maven-publish'
    id "com.github.johnrengelman.shadow" version "8.0.0"
    id "org.cadixdev.licenser" version "0.6.1"
}

var baseVersion = "0.0"

sourceCompatibility = 1.8
group = 'net.flintloader'
version = '${baseVersion}.0'
archivesBaseName = "flint-installer"

def ENV = System.getenv()

if (ENV.BUILD_NUMBER) {
    var build = (ENV.BUILD_NUMBER.toInteger() - 1)
    version = "${baseVersion}.${build}"
} else {
    version = "${baseVersion}.local"
}

repositories {
    maven {
        name = 'Flint Mirror'
        url = 'https://maven.flintloader.net/mirror'
    }
    mavenCentral()
}

def nativeLibVersion = "0.1.3"
def nativeLibDistributions = [
        "windows-ARM64", "windows-Win32", "windows-x64", "macos-x86_64_arm64"
]

dependencies {
    implementation ('org.sharegov:mjson:1.4.1') {
        transitive false
    }
    nativeLibDistributions.each {
        implementation "net.fabricmc.fabric-installer-native-lib:${it}:${nativeLibVersion}"
    }

    testImplementation 'junit:junit:4.13.2'
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"

    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = 8
    }
}

shadowJar {
    manifest {
        attributes 'Implementation-Title': 'FlintInstaller',
                'Implementation-Version': project.version,
                'Main-Class': 'net.flintloader.installer.Main'
    }

    minimize()
    archiveClassifier.set(null)
    exclude('icon.ico')
}

jar {
    enabled = false
}

license {
    header rootProject.file("HEADER")
    include "**/*.java"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version

            artifact (shadowJar) {
                classifier null
            }
        }
    }
    repositories {
        maven {
            if (ENV.MAVEN_URL) {
                url ENV.MAVEN_URL
                credentials {
                    username ENV.MAVEN_USER
                    password ENV.MAVEN_PASS
                }
            }
        }
    }
}